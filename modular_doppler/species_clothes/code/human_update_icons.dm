/**
 * Returns a mutable appearance of [clothing], adjusted for bodyshape and offsets. Effectively the final step in update icons for clothing.
 *
 * Args:
 * * obj/item/clothing: The clothing item we are getting a new appearance for.
 * * layer: The layer the resultant appearance will be using.
 * * slot: The item slot of the clothing item. OFFSET_UNIFORM, etc
 * * icon_file: The DEFAULT icon file that will be used. Will be overridden by bodyshape modifiers if applicable!
 * * datum/worn_feature_offset/offset: The offset to be applied to the resultant appearance, if no bodyshape override was found/created. Nullable.
 *
 * Returns:
 * * A /mutable_appearance, either from a cache or newly created, adjusted by bodyshape and offsets.
 */
/mob/living/carbon/human/proc/get_updated_worn_icon(obj/item/clothing, layer, slot, icon_file, datum/worn_feature_offset/offset)
	RETURN_TYPE(/mutable_appearance)

	var/found_special_sprite = FALSE
	for(var/shape in clothing.supported_bodyshapes)
		if(!(bodyshape & shape))
			continue
		var/potential_file = clothing.bodyshape_icon_files["[shape]"]
		if (icon_exists(potential_file, clothing.icon_state))
			icon_file = clothing.bodyshape_icon_files["[shape]"]
			if (shape == BODYSHAPE_HUMANOID) // EVERYTHING has this
				continue
			found_special_sprite = TRUE
			break

	var/autogen_override = FALSE
	if(!found_special_sprite)
		var/corresponding_shape = clothing.get_matching_bodyshape(src)
		if (!isnull(corresponding_shape))
			// we found a config for our body? that must mean the clothing doesnt fit. so just trust it
			var/autogenerated_icon = clothing.generate_autogen_worn_icon(corresponding_shape, slot)
			if(autogenerated_icon)
				icon_file = autogenerated_icon
				autogen_override = TRUE

	var/icon/final_icon = clothing.build_worn_icon(default_layer = layer, default_icon_file = icon_file, override_file = autogen_override ? icon_file : null, humie = src)

	if (!autogen_override && !found_special_sprite)
		offset?.apply_offset(final_icon)

	return final_icon
